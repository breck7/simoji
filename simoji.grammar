anyCell
booleanCell
stringCell
 highlightScope string
settingValueCell
 highlightScope constant.numeric
cssCell
 highlightScope string
javascriptCell
 highlightScope string
htmlCell
 highlightScope string
emojiCell
 highlightScope string
ohayoCell
 highlightScope string
blankCell
codeCell
 highlightScope comment
commentCell
 highlightScope comment
keywordCell
 highlightScope keyword
textCell
 highlightScope string
integerCell
 highlightScope constant.numeric
behaviorNameCell
 highlightScope keyword
conditionalOperatorCell
 highlightScope keyword
 enum < > = <= >=
positionCell
 highlightScope constant.numeric
neighborCountCell
 extends integerCell
 min 0
 max 8
integerOrPercentCell
 highlightScope constant.numeric
probabilityCell
 description A number between 0 and 1
 highlightScope constant.numeric
propertyNameCell
 highlightScope keyword
angleCell
 enum North South East West NorthWest NorthEast SouthWest SouthEast
 highlightScope constant.numeric
errorNode
 baseNodeType errorNode
simojiNode
 extensions simoji
 description A Tree Language that compiles to a TreeComponentFramework app.
 root
 inScope abstractIgnoreNode abstractSetupNode abstractInjectCommandNode onTickNode onExtinctNode behaviorDefinitionNode experimentNode settingDefinitionNode
 catchAllNodeType agentDefinitionNode
 compilesTo javascript
 example
  ðŸ¦‹
   onTick .1
    turnRandomly
    move
   onTick .2
    turnToward ðŸ’¡
    move
  ðŸ’¡
  
  insert 10 ðŸ¦‹
  insert 2 ðŸ’¡
 javascript
  get agentTypes() {
   return this.filter(node => node.getNodeTypeId() === "agentDefinitionNode")
  }
experimentNode
 crux experiment
 cells keywordCell
 inScope abstractIgnoreNode abstractSetupNode abstractInjectCommandNode onTickNode onExtinctNode settingDefinitionNode
 catchAllCellType stringCell
abstractSetupNode
atTimeNode
 crux atTime
 description Run commands at a certain tick.
 cells keywordCell integerCell
 extends abstractSetupNode
 inScope abstractInjectCommandNode
abstractSetupNumberNode
 cells keywordCell integerCell
 extends abstractSetupNode
 javascript
  compile() {
   return ""
  }
gridSizeNode
 description Size of a grid cell in pixels. Min is 10. Max is 200.
 extends abstractSetupNumberNode
 crux size
rowsNode
 description Number of rows in the grid. Default is based on screen size.
 extends abstractSetupNumberNode
 crux rows
columnsNode
 description Number of columns in the grid. Default is based on screen size.
 extends abstractSetupNumberNode
 crux columns
seedNode
 description If you'd like reproducible runs set a seed for the random number generator.
 extends abstractSetupNumberNode
 crux seed
ticksPerSecondNode
 description Time in milliseconds of one step.
 extends abstractSetupNumberNode
 crux ticksPerSecond
reportNode
 crux report
 description Define a custom report template.
 catchAllNodeType ohayoLineNode
 extends abstractSetupNode
 cells keywordCell
 javascript
  compile() {
   return ""
  }
styleNode
 description Optional CSS to load in BoardStyleComponent
 extends abstractSetupNode
 cells keywordCell
 crux style
 catchAllNodeType styleLineNode
 javascript
  compile() {
   return ""
  }
questionNode
 crux question
 description What are you trying to figure out?
 cells keywordCell
 catchAllCellType stringCell
 extends abstractSetupNode
abstractInjectCommandNode
fillNode
 description Fill all blank cells with this agent.
 extends abstractInjectCommandNode
 cells keywordCell emojiCell
 crux fill
drawNode
 extends abstractInjectCommandNode
 cells keywordCell
 crux draw
 catchAllNodeType drawLineNode
insertNode
 extends abstractInjectCommandNode
 cells keywordCell integerOrPercentCell emojiCell
 crux insert
insertAtNode
 extends insertNode
 description Insert at X Y
 cells keywordCell emojiCell positionCell positionCell
 crux insertAt
insertClusterNode
 extends insertNode
 crux insertCluster
 catchAllCellType integerCell
rectangleDrawNode
 extends abstractInjectCommandNode
 cells keywordCell emojiCell integerCell integerCell
 catchAllCellType integerCell
 crux rectangle
pasteDrawNode
 extends abstractInjectCommandNode
 cells keywordCell
 crux paste
 catchAllNodeType pasteLineNode
drawLineNode
 catchAllCellType emojiCell
pasteLineNode
 catchAllCellType anyCell
 catchAllNodeType pasteLineNode
agentDefinitionNode
 inScope abstractIgnoreNode abstractEventNode abstractAgentAttributeNode abstractBehaviorAttributeNode
 cells keywordCell
 catchAllNodeType errorNode
 compiler
  stringTemplate 
 javascript
  compile() {
   const root = this.getRootNode()
   const name = root.agentKeywordMap[this.getWord(0)]
   const normal = super.compile()
   const behaviors = this.filter(node => node.getNodeTypeId() === "abstractBehaviorAttributeNode")
    .map(behavior => `"${behavior.getLine()}"`)
    .join(",")
   return `class ${name} extends Agent {
      icon = "${this.getWord(0)}"
      behaviors = [${behaviors}]
      ${normal}
    }`
  }
abstractCommandNode
 cells keywordCell
abstractSubjectObjectCommandNode
 extends abstractCommandNode
replaceWithCommandNode
 extends abstractSubjectObjectCommandNode
 crux replaceWith
 cells keywordCell emojiCell
kickItCommandNode
 extends abstractSubjectObjectCommandNode
 crux kickIt
shootCommandNode
 extends abstractSubjectObjectCommandNode
 crux shoot
pickItUpCommandNode
 extends abstractSubjectObjectCommandNode
 crux pickItUp
spawnCommandNode
 crux spawn
 extends abstractCommandNode
 cells keywordCell emojiCell
 catchAllCellType positionCell
moveToEmptySpotCommandNode
 crux moveToEmptySpot
 extends abstractCommandNode
 cells keywordCell
removeCommandNode
 description Remove this agent from the board.
 crux remove
 extends abstractCommandNode
 cells keywordCell
javascriptCommandNode
 description An escape hatch so you can write custom javascript in a pinch.
 extends abstractCommandNode
 crux javascript
 catchAllNodeType javascriptLineNode
 cells keywordCell
alertCommandNode
 extends abstractCommandNode
 crux alert
 catchAllCellType stringCell
logCommandNode
 extends abstractCommandNode
 crux log
 catchAllCellType stringCell
narrateCommandNode
 extends abstractCommandNode
 crux narrate
 catchAllCellType stringCell
pauseCommandNode
 extends abstractCommandNode
 crux pause
decreaseCommandNode
 extends abstractCommandNode
 description Decrease a property by 1.
 crux decrease
 cells keywordCell propertyNameCell
increaseCommandNode
 extends abstractCommandNode
 description Increase a property by 1.
 crux increase
 cells keywordCell propertyNameCell
moveCommandNode
 extends abstractCommandNode
 crux move
turnRandomlyCommandNode
 extends abstractCommandNode
 crux turnRandomly
jitterCommandNode
 extends abstractCommandNode
 crux jitter
turnTowardCommandNode
 description Turn to the closest agent of a certain type.
 extends abstractCommandNode
 crux turnToward
 cells keywordCell emojiCell
turnFromCommandNode
 description Turn away from the closest agent of a certain type.
 extends abstractCommandNode
 crux turnFrom
 cells keywordCell emojiCell
learnCommandNode
 crux learn
 extends abstractCommandNode
 cells keywordCell behaviorNameCell
unlearnCommandNode
 crux unlearn
 extends abstractCommandNode
 cells keywordCell behaviorNameCell
abstractAgentAttributeNode
 cells keywordCell
abstractStringAttributeNode
 extends abstractAgentAttributeNode
 pattern ^\w+ .+$
 catchAllCellType stringCell
 javascript
  compile() {
   return `${this.getWord(0)} = "${this.getWord(1)}"`
  }
angleNode
 extends abstractStringAttributeNode
 cells keywordCell angleCell
 crux angle
agentStyleNode
 description Provide custom CSS for an agent type.
 extends abstractStringAttributeNode
 cells keywordCell cssCell
 crux style
agentHtmlNode
 description Provide custom HTML for each rendered agent.
 extends abstractStringAttributeNode
 cells keywordCell htmlCell
 crux html
abstractBooleanAttributeNode
 description A boolean attribute.
 extends abstractAgentAttributeNode
 javascript
  compile() {
   return `${this.getWord(0)} = true`
  }
noPaletteNode
 extends abstractBooleanAttributeNode
 cruxFromId
 description Don't show this agent in the palette.
solidTraitNode
 description If set other agents won't pass through these.
 extends abstractBooleanAttributeNode
 crux solid
bouncyTraitNode
 description If set other agents will bounce off this after a collision.
 extends abstractBooleanAttributeNode
 crux bouncy
abstractIntegerAttributeNode
 extends abstractAgentAttributeNode
 description An integer attribute.
 cells keywordCell integerCell
 javascript
  compile() {
   return `${this.getWord(0)} = ${this.getWord(1)}`
  }
customIntegerAttributeNode
 pattern ^\w+ \d+$
 extends abstractIntegerAttributeNode
healthNode
 extends abstractIntegerAttributeNode
 cruxFromId
sizeNode
 extends abstractIntegerAttributeNode
 description Size of the agent's rectangle.
 cruxFromId
settingDefinitionNode
 description Define a configurable input.
 cells keywordCell settingValueCell
 pattern ^\w+Setting .+$
ohayoLineNode
 description Data visualization code written for Ohayo.
 catchAllCellType ohayoCell
styleLineNode
 catchAllCellType cssCell
 catchAllNodeType styleLineNode
targetEmojiNode
 inScope abstractCommandNode
 cells emojiCell
abstractEventNode
 cells keywordCell
 catchAllCellType probabilityCell
 javascript
  compile() {
   return ``
  }
abstractInteractionEventNode
 extends abstractEventNode
 catchAllNodeType targetEmojiNode
onHitNode
 extends abstractInteractionEventNode
 crux onHit
 description Define what happens when this agent collides with other agents.
onTouchNode
 extends abstractInteractionEventNode
 crux onTouch
 description Define what happens when this agent is adjacent to other agents.
onNeighborsNode
 description Define what happens when a certain amount of neighbors are nearby.
 extends abstractInteractionEventNode
 inScope emojiAndNeighborConditionNode
 crux onNeighbors
onDeathNode
 extends abstractEventNode
 crux onDeath
 inScope abstractCommandNode
 description Define what happens when this agent runs out of health.
onTickNode
 extends abstractEventNode
 crux onTick
 inScope abstractCommandNode
 description Define what happens each tick.
emojiAndNeighborConditionNode
 inScope abstractCommandNode
 pattern ^.+ (<|>|=|<=|>=)+ .+$
 cells emojiCell conditionalOperatorCell neighborCountCell
onExtinctNode
 crux onExtinct
 inScope abstractCommandNode
 cells keywordCell emojiCell
 description Define what happens when a type of agent goes extinct from the board.
 javascript
  compile() {
   return ""
  }
abstractIgnoreNode
 tags doNotSynthesize
 javascript
  compile () {
    return ""
  }
commentNode
 extends abstractIgnoreNode
 catchAllCellType commentCell
 crux comment
 catchAllNodeType commentLineNode
commentAliasNode
 description Alternate alias for a comment.
 crux #
 extends commentNode
blankLineNode
 extends abstractIgnoreNode
 description Blank lines compile do nothing.
 cells blankCell
 pattern ^$
commentLineNode
 catchAllCellType commentCell
javascriptLineNode
 catchAllCellType javascriptCell
abstractBehaviorAttributeNode
 cells behaviorNameCell
 pattern ^.*Behavior$
 javascript
  compile() {
   return ""
  }
behaviorDefinitionNode
 inScope abstractIgnoreNode abstractEventNode
 cells behaviorNameCell
 pattern ^.*Behavior$
 catchAllNodeType errorNode
 javascript
  compile() {
   return ""
  }